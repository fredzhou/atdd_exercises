*** Settings ***
Test Setup        Run Keywords    Open Browser    ${SERVER}    AND    DeletePeople
Test Teardown     Run Keywords    DeletePeople    AND    Close Browser
Test Template     CreatePersonTemplate
Resource          PersonResource.txt

*** Test Cases ***
Successfully Create A Person
#   Name        Title    Result
    Lv Yi       Coach    Lv Yi, Coach has been created!

Successfully Create Multiple Person
#   Name         Title        Result
    Lv Yi        Coach        Lv Yi, Coach has been created!
    Zhou Feng    Developer    Zhou Feng, Developer has been created!
    Zhang Eric   Developer    Zhang Eric, Developer has been created!

Not Create Same Person
#   Name        Title      Result
    Lv Yi       Coach      Lv Yi, Coach has been created!
    Lv Yi       Developer  Lv Yi already exists!

*** Keywords ***
CreatePersonTemplate
    [Arguments]    ${name}    ${title}    ${output}
    CreatePerson    ${name}    ${title}
    VerifyOutput    ${output}
    PersonExists    ${name}
